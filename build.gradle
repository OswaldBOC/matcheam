buildscript {
    ext {
        springBootVersion = '1.4.2.RELEASE'
    }
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'com.h2database:h2:1.4.193'
        classpath 'nu.studer:gradle-jooq-plugin:2.0.3'
        classpath 'mysql:mysql-connector-java:5.1.40'
    }

}
plugins {
    id "org.flywaydb.flyway" version "4.1.0"
}
flyway {
    url = 'jdbc:h2:tcp://localhost:9092/matcheam;MODE=MySQL;INIT=CREATE SCHEMA IF NOT EXISTS matcheam;'
    user = 'sa'
}
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'nu.studer.jooq'


jar {
    baseName = 'matcheam'
    version = '0.0.1-SNAPSHOT'
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter')
    compile("org.springframework.boot:spring-boot-starter-web")
    compile('org.thymeleaf:thymeleaf-spring4')
    compile('org.thymeleaf.extras:thymeleaf-extras-java8time')
    compile("com.h2database:h2")
    compile("org.springframework:spring-jdbc")
    compile("mysql:mysql-connector-java")
    testCompile('org.springframework.boot:spring-boot-starter-test')

    compile group: 'org.jooq', name: 'jooq', version: '3.9.1'
    compile group: 'org.jooq', name: 'jooq-meta', version: '3.9.1'

    jooqRuntime 'com.h2database:h2:1.4.193'
}

jooq {
    version = '3.9.1'
    matcheam(sourceSets.main) {
        jdbc {
            driver = 'org.h2.Driver'
            url = 'jdbc:h2:tcp://localhost:9092/matcheam;MODE=MySQL;'
            user = 'sa'
            password = ''
        }
        generator {
            database {
                name = 'org.jooq.util.h2.H2Database'
                inputSchema = 'matcheam'
                forcedTypes {
                    forcedType {
                        name = 'DECIMAL'
                        expression = '.*'
                        types = 'DECIMAL.*'
                    }
                }
            }
            target {
                packageName = 'matcheam.jooq.generate'
                directory = 'src/main/java'
            }
	        generate {
	        	javaTimeTypes = true
	        }
        }
    }
}
